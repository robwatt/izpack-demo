tasks.register('copyIzpackResources', Copy) {
    dependsOn jar

    from layout.projectDirectory.dir('izpack/resources')
    into layout.buildDirectory.dir('assemble/izpack')
}

tasks.register('copyProductZip', Copy) {
    dependsOn copyIzpackResources

    from { project(':product').layout.buildDirectory.file("distributions/product${version}.zip")}
    into layout.buildDirectory.dir('assemble/izpack')
    rename "product${version}.zip", "product.zip"
}

tasks.register('copyProcessJar', Copy) {
    dependsOn copyProductZip

    from layout.buildDirectory.file("libs/izpack-process-${version}.jar")
    into layout.buildDirectory.dir('assemble/izpack')
    rename "izpack-process-${version}.jar", "izpack-process.jar"
}

izpack {
    baseDir = layout.buildDirectory.dir('assemble/izpack').get().asFile
    installFile = file('izpack/installer.xml')
    outputFile = layout.buildDirectory.file("distributions/izpack-demo-${version}-installer.jar").get().asFile
    compression = 'deflate'
    compressionLevel = 9
    appProperties = ['app.group': 'org.raw', 'app.name': 'izpack-demo', 'app.title': 'izPack Demo',
                     'app.version': version, 'app.subpath': "izpack-demo-$version"]
}
